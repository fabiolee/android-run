apply plugin: 'com.android.feature'
apply plugin: 'io.fabric'
apply plugin: 'jacoco'

def keystorePropertiesFile = rootProject.file("cert/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        multiDexEnabled = true
        testInstrumentationRunner "com.blogspot.carirunners.run.util.AppTestRunner"
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    buildTypes {
        debug {
            testCoverageEnabled !project.hasProperty('android.injected.invoked.from.ide')
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }
    lintOptions {
        lintConfig rootProject.file('lint.xml')
    }
    baseFeature = true
}

crashlytics {
    instantAppSupport true
}

jacoco {
    toolVersion = "0.7.4+"
}

dependencies {
    application project(':app')
    implementation "android.arch.lifecycle:common-java8:$android_arch_version"
    implementation "android.arch.lifecycle:extensions:$android_arch_version"
    implementation "android.arch.lifecycle:runtime:$android_arch_version"
    implementation "android.arch.persistence.room:runtime:$android_arch_version"
    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    api "com.android.support:cardview-v7:$androidSupportVersion"
    api "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support:multidex:$androidMultidexVersion"
    implementation "com.android.support:preference-v14:$androidSupportVersion"
    implementation "com.android.support:recyclerview-v7:$androidSupportVersion"
    implementation("com.crashlytics.sdk.android:crashlytics:$fabric_crashlytics_version@aar") {
        transitive = true;
    }
    implementation "com.facebook.stetho:stetho:$facebookStethoVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$squareOkHttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$squareRetrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$squareRetrofitVersion"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.google.android.instantapps:instantapps:$androidInstantAppsVersion"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    implementation "com.google.firebase:firebase-core:$googleServicesVersion"
    api "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    api "com.jakewharton.timber:timber:$timber_version"
    implementation "org.jsoup:jsoup:$jsoupVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$android_arch_version"
    annotationProcessor "android.arch.lifecycle:compiler:$android_arch_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    testImplementation("android.arch.core:core-testing:$android_arch_version", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })
    testImplementation "junit:junit:$junit_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$mockwebserver_version"

    androidTestImplementation "android.arch.core:core-testing:$android_arch_version"
    androidTestImplementation "com.android.support:appcompat-v7:$androidSupportVersion"
    androidTestImplementation "com.android.support:recyclerview-v7:$androidSupportVersion"
    androidTestImplementation "com.android.support:support-v4:$androidSupportVersion"
    androidTestImplementation "com.android.support:design:$androidSupportVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
}

task fullCoverageReport(type: JacocoReport) {
    dependsOn 'createDebugCoverageReport'
    dependsOn 'testDebugUnitTest'
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
                      '**/*Test*.*', 'android/**/*.*',
                      '**/*_MembersInjector.class',
                      '**/Dagger*Component.class',
                      '**/Dagger*Component$Builder.class',
                      '**/*_*Factory.class',
                      '**/*ComponentImpl.class',
                      '**/*SubComponentBuilder.class']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ])
}

apply plugin: 'com.google.gms.google-services'
